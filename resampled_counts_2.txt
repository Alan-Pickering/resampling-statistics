 ﻿' Some students have been studying a course live, and some on TV.
' At the end, they are asked whether they would recommend the course.
' 24 watched it live.  Of these, 20 sould recommend it.
' 9 watched it on TV.  Of these, 6 would recommend it.
' Is the proportion of recommendations significantly different in the two conditions?
' We could investigate this with a chi-square test.
' Or (using resampling) we could simply say
'   Watching live, the proportion who would recommend is 0.83
'   Watching on TV, the proportion who would recommend is 0.67
'   The difference in proportions is 0.16
'   Is this significantly different from 0 (expected under the null hypothesis
'     that the mode of delivery makes no difference)

' For explanation of how the script works, see separate file.
' Note that by convention, $ represents a simulated group.

URN 26#1 7#0 students

REPEAT 1000

  SHUFFLE students shuffstudents  
  TAKE shuffstudents 1,24 live$
  TAKE shuffstudents 25,33 tv$  

  COUNT live$ = 1 liveyes$
  DIVIDE liveyes$ 24  prlive$

  COUNT tv$ =1 tvyes$
  DIVIDE tvyes$ 9 prtv$

  SUBTRACT prlive$ prtv$ diff$

  SCORE diff$ scrboard

END
COUNT scrboard >=0.16 more

HISTOGRAM scrboard
DIVIDE more 1000 prob
PRINT prob</div></pre>